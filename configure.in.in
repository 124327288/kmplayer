#MIN_CONFIG(3)

AM_INIT_AUTOMAKE(kmplayer,0.8)

if test "$build_arts" = "no"; then
    not_want_arts_volume="yes"
else
        AC_MSG_CHECKING(if arts volume support is wanted)
        AC_ARG_ENABLE(arts-volume,
                [  --disable-arts-volume   disable arts volume support],
                [if test "$enableval" != yes; then
                       AC_MSG_RESULT(no)
                       not_want_arts_volume="yes"
                 else
                       AC_MSG_RESULT(yes)
                 fi], [AC_MSG_RESULT(yes); not_want_arts_volume="no"])
fi

if test "$not_want_arts_volume" != "yes"; then
        AC_DEFINE(USE_ARTS, 1, [If we use arts volume])
        LIB_ARTS="-lartskde"
        AC_SUBST(LIB_ARTS)
fi

AC_MSG_CHECKING(if koffice-plugin support is wanted)
AC_ARG_ENABLE(koffice-plugin,
[  --enable-koffice-plugin enable koffice plugin],
[if test "$enableval" = yes; then
        AC_MSG_RESULT(yes)
        want_koffice_plugin="yes"
else
        AC_MSG_RESULT(no)
fi], [AC_MSG_RESULT(no); want_koffice_plugin="no"])

KDE_CHECK_HEADER(koDocument.h,
  have_koffice=yes,
  have_koffice=no)

AC_MSG_CHECKING([if kmplayer can be compiled with koffice support])
AC_MSG_RESULT($have_koffice)

AM_CONDITIONAL(include_koffice_support, test "$want_koffice_plugin" = "yes" -a "$have_koffice" = "yes")
if test "$want_koffice_plugin" = "yes" -a "$have_koffice" = "yes"; then
        AC_DEFINE(HAVE_KOFFICE, 1, [If we have koffice installed])
        LIB_KOFFICE="-lkofficecore -lkofficeui"
        AC_SUBST(LIB_KOFFICE)
fi

KDE_CHECK_HEADER(xine.h,
   have_xine=yes,
   have_xine=no)

AC_MSG_CHECKING([if kxineplayer can be compiled])
AC_MSG_RESULT($have_xine)

if test "$have_xine" = "yes"; then
  vers=`xine-config --version 2>/dev/null | awk 'BEGIN { FS = "."; } { printf "%d", ($1 * 1000 + $2) * 1000 + $3;}'`
  if test -n "$vers" && test "$vers" -ge 1000000
  then
        AC_DEFINE(HAVE_XINE, 1, [If we have libxine installed])
        LIB_XINE="`xine-config --libs`"
        AC_SUBST(LIB_XINE)
        CFLAGS_XINE="`xine-config --cflags`"
        AC_SUBST(CFLAGS_XINE)
  else
       have_xine="no"
       AC_MSG_WARN([Your xine installation is too old (1.0.0 or later required)])
  fi
fi

AM_CONDITIONAL(include_kxineplayer, test "$have_xine" = "yes")
