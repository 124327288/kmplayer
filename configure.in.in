#AM_INIT_AUTOMAKE(kmplayer,0.10.0c)

#KDE_ENABLE_HIDDEN_VISIBILITY

AC_ARG_VAR(PKGCONFIGFOUND, [Path to pkg-config])
AC_CHECK_PROG(PKGCONFIGFOUND, pkg-config,[yes])

AC_MSG_CHECKING(if koffice-plugin support is wanted)
AC_ARG_ENABLE(koffice-plugin,
[  --enable-koffice-plugin enable koffice plugin],
[if test "$enableval" = yes; then
        AC_MSG_RESULT(yes)
        want_koffice_plugin="yes"
else
        AC_MSG_RESULT(no)
fi], [AC_MSG_RESULT(no); want_koffice_plugin="no"])

KDE_CHECK_HEADER(koDocument.h,
  have_koffice=yes,
  have_koffice=no)

AC_MSG_CHECKING([if kmplayer can be compiled with koffice support])
AC_MSG_RESULT($have_koffice)

AM_CONDITIONAL(include_koffice_support, test "$want_koffice_plugin" = "yes" -a "$have_koffice" = "yes")
if test "$want_koffice_plugin" = "yes" -a "$have_koffice" = "yes"; then
        AC_DEFINE(HAVE_KOFFICE, 1, [If we have koffice installed])
        LIB_KOFFICE="-lkofficecore -lkofficeui"
        AC_SUBST(LIB_KOFFICE)
fi

AC_MSG_CHECKING(if expat XML parsing is wanted)
AC_ARG_ENABLE(expat,
[  --enable-expat use expat libs],
[if test "$enableval" = yes; then
        AC_MSG_RESULT(yes)
        want_expat="yes"
else
        AC_MSG_RESULT(no)
fi], [AC_MSG_RESULT(no); want_expat="no"])

KDE_CHECK_HEADER(expat.h,
  have_expat=yes,
  have_expat=no)

AC_MSG_CHECKING([if kmplayer can use expat its XML parser])
AC_MSG_RESULT($have_expat)

AM_CONDITIONAL(include_expat_support, test "$want_expat" = "yes" -a "$have_expat" = "yes")
if test x$have_expat = xyes -a x$want_expat = xyes; then
    AC_DEFINE(HAVE_EXPAT, 1, [If libexpat is installed])
    LIB_EXPAT="-lexpat"
    AC_SUBST(LIB_EXPAT)
fi

KDE_CHECK_HEADER(xine.h,
   have_xine=yes,
   have_xine=no)

AC_MSG_CHECKING([if kxineplayer can be compiled])
AC_MSG_RESULT($have_xine)

AC_ARG_WITH(xine,
    AC_HELP_STRING([--without-xine],[build KMPlayer without Xine [default=with]]),
    [build_xine=$withval],
    [build_xine=yes]
)

if test "$build_xine" != "no"; then
    if test "$have_xine" = "yes"; then
      vers=`xine-config --version 2>/dev/null | awk 'BEGIN { FS = "."; } { printf "%d", ($1 * 1000 + $2) * 1000 + $3;}'`
      if test -n "$vers" && test "$vers" -ge 1000000
      then
            AC_DEFINE(HAVE_XINE, 1, [If we have libxine installed])
            LIB_XINE="`xine-config --libs`"
            AC_SUBST(LIB_XINE)
            CFLAGS_XINE="`xine-config --cflags`"
            AC_SUBST(CFLAGS_XINE)
      else
           have_xine="no"
           AC_MSG_WARN([Your xine installation is too old (1.0.0 or later required)])
      fi
    fi
else
    have_xine="no"
fi

AM_CONDITIONAL(include_kxineplayer, test "$have_xine" = "yes")

AC_ARG_WITH(gstreamer,
    AC_HELP_STRING([--without-gstreamer],[build KMPlayer without GStreamer [default=with]]),
    [build_gstreamer=$withval],
    [build_gstreamer=yes]
)

if test "$build_gstreamer" != "no"; then
    if test "$PKGCONFIGFOUND" = "yes" ; then
        # check for GStreamer
        dnl Now we're ready to ask for gstreamer libs and cflags
        dnl And we can also ask for the right version of gstreamer

        GST_MAJORMINOR=0.10
        GST_REQ=0.10.0

        PKG_CHECK_MODULES(GST, gstreamer-$GST_MAJORMINOR >= $GST_REQ,
            have_gst=yes,have_gst=no)

        PKG_CHECK_MODULES(GST_PLUGINS, gstreamer-plugins-base-$GST_MAJORMINOR >= $GST_REQ, \
            have_gst_plugins=yes,have_gst_plugins=no)
        LIB_GST=""
        CFLAGS_GST=""
        LIB_GST_PLUGINS=""
        dnl Give error if we don't have gstreamer
        if test "x$have_gst" = "xyes"; then
            AC_SUBST(GST_MAJORMINOR)
            AC_DEFINE(HAVE_GSTREAMER, 1, [have GStreamer])
            LIB_GST=$GST_LIBS
            CFLAGS_GST=$GST_CFLAGS
            LIB_GST_PLUGINS=$GST_INTERFACES_LIBS
        fi
        AC_SUBST(LIB_GST)
        AC_SUBST(CFLAGS_GST)
        AC_SUBST(LIB_GST_PLUGINS)
    fi
fi
AC_MSG_CHECKING([if kgstplayer can be compiled])
if test "x$have_gst" = "xyes" && test "x$have_gst_plugins" = "xyes"; then
    AC_MSG_RESULT(yes)
else
    AC_MSG_RESULT(no)
fi

AM_CONDITIONAL(include_kgstplayer, [test "x$have_gst" = "xyes" && test "x$have_gst_plugins" = "xyes"])

buildnpp=yes
LIBNSPR_LIBS=""
LIBNSPR_CFLAGS=""
AC_ARG_ENABLE(nspr, [  --enable-npplayer    Enables building with npp support],
[if test "x$enableval" == "xno"; then
        buildnpp=no
    fi])
if test "$buildnpp" = "yes"; then

    ########### Check for DBus

    AC_MSG_CHECKING(for DBus)

    dbus_inc=NOTFOUND
    dbus_lib=NOTFOUND
    dbus=NOTFOUND

    search_incs="$kde_includes $kde_includes/dbus-1.0 /usr/include /usr/include/dbus-1.0 /usr/local/include /usr/local/include/dbus-1.0"
    AC_FIND_FILE(dbus/dbus.h, $search_incs, dbus_incdir)

    search_incs_arch_deps="$kde_includes /usr/lib64/dbus-1.0/include /usr/lib/dbus-1.0/include /usr/local/lib/dbus-1.0/include"
    AC_FIND_FILE(dbus/dbus-arch-deps.h, $search_incs_arch_deps, dbus_incdir_arch_deps)

    if [test -r $dbus_incdir/dbus/dbus.h] && [test -r $dbus_incdir_arch_deps/dbus/dbus-arch-deps.h] ; then
      DBUS_INCS="-I$dbus_incdir -I$dbus_incdir_arch_deps"
      dbus_inc=FOUND
    fi

    search_libs="$kde_libraries /usr/lib64 /usr/lib /usr/local/lib /lib /lib64"
    AC_FIND_FILE(libdbus-1.so, $search_libs, dbus_libdir)

    if test -r $dbus_libdir/libdbus-1.so ; then
      DBUS_LIBS="-L$dbus_libdir -ldbus-1"
      dbus_lib=FOUND
    fi

    if [test $dbus_inc = FOUND] && [test $dbus_lib = FOUND] ; then
      AC_MSG_RESULT(headers $dbus_incdir $dbus_incdir_arch_deps  libraries $dbus_libdir)
      dbus=FOUND
    else
      AC_MSG_RESULT(searched but not found)
    fi

    AC_SUBST(DBUS_INCS)
    AC_SUBST(DBUS_LIBS)

    ########### Check for DBus-Qt bindings

    AC_MSG_CHECKING(for DBus-Qt bindings)

    dbusqt_inc=NOTFOUND
    dbusqt_lib=NOTFOUND
    dbusqt=NOTFOUND

    search_incs="$kde_includes $kde_includes/dbus-1.0 /usr/include /usr/include/dbus-1.0 /usr/local/include /usr/local/include/dbus-1.0"
    AC_FIND_FILE(dbus/connection.h, $search_incs, dbusqt_incdir)

    if test -r $dbusqt_incdir/dbus/connection.h ; then
      have_qt_patch=0
      grep dbus_connection_setup_with_qt_main $dbusqt_incdir/dbus/connection.h \
      > /dev/null 2>&1 && have_qt_patch=1
      if test $have_qt_patch = 1 ; then
        DBUSQT_INCS="-I$dbusqt_incdir"
        dbusqt_inc=FOUND
      fi
    fi

    search_libs="$kde_libraries /usr/lib /usr/lib64 /usr/local/lib"
    AC_FIND_FILE(libdbus-qt-1.so, $search_libs, dbusqt_libdir)

    if test -r $dbusqt_libdir/libdbus-qt-1.so ; then
      DBUSQT_LIBS="-L$dbusqt_libdir -ldbus-qt-1"
      dbusqt_lib=FOUND
    fi

    if [test $dbusqt_inc = FOUND] && [test $dbusqt_lib = FOUND] ; then
      AC_MSG_RESULT(headers $dbusqt_incdir libraries $dbusqt_libdir)
      dbusqt=FOUND
    else
      AC_MSG_RESULT(searched but not found)
    fi

    AC_SUBST(DBUSQT_INCS)
    AC_SUBST(DBUSQT_LIBS)

    if test "$dbusqt" = "FOUND" ; then
            AC_DEFINE(HAVE_DBUS, 1, [have D-Bus])
            have_nspr=yes
            AC_DEFINE(HAVE_NSPR, 1, [build Netscape plugin loader])
            LIBNSPR_LIBS="`$PKG_CONFIG --libs gtk+-x11-2.0` `$PKG_CONFIG --libs dbus-glib-1` `$PKG_CONFIG --libs gthread-2.0`"
            LIBNSPR_CFLAGS="`$PKG_CONFIG --cflags gtk+-x11-2.0` `$PKG_CONFIG --cflags dbus-glib-1`"
    fi
fi
AC_SUBST(LIBNSPR_LIBS)
AC_SUBST(LIBNSPR_CFLAGS)

AM_CONDITIONAL(include_knpplayer, test "$have_nspr" = "yes")

hascairo=yes
LIBCAIRO_LIBS=""
LIBCAIRO_CFLAGS=""
AC_ARG_ENABLE(cairo, [  --enable-cairo     Enables building with cairo support],
[if test "x$enableval" == "xno"; then
        hascairo=no
    fi])
if test $hascairo = yes; then
        AC_MSG_CHECKING([Checking for cairo with pkg-config])
        hascairo=no
        if test -n "$PKG_CONFIG"; then
                LIBCAIRO_LIBS="`$PKG_CONFIG cairo --libs`"
                LIBCAIRO_CFLAGS="`$PKG_CONFIG cairo --cflags`"
                if test -n "$LIBCAIRO_LIBS" || test -n "$LIBCAIRO_CFLAGS"; then
                        AC_MSG_RESULT([found])
                        hascairo=yes
                else
                        AC_MSG_RESULT([not found])
                fi
        else
                AC_MSG_RESULT([failed: pkg-config not found])
        fi
        if test $hascairo = yes; then
                AC_DEFINE_UNQUOTED(HAVE_CAIRO, 1, [Defines if your system has the cairo library])
        else
                AC_MSG_WARN([Couldn't find a usable cairo])
        fi
fi
AC_SUBST(LIBCAIRO_LIBS)
AC_SUBST(LIBCAIRO_CFLAGS)

KDE_CHECK_BINUTILS

AC_MSG_CHECKING([for KDE version])

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
kdeversion_save_CXXFLAGS="$CXXFLAGS"
kdeversion_safe_LIBS="$LIBS"
LIBS="$LIBS $X_EXTRA_LIBS"
CXXFLAGS="$CXXFLAGS $all_includes"

AC_COMPILE_IFELSE([
#include <kdeversion.h>
#if ! ( KDE_IS_VERSION( 3, 3, 91 ) )
#error KDE 3.3
#endif
],
    need_kde33_compat="no"
,
    need_kde33_compat="yes"
)

AC_COMPILE_IFELSE([
#include <kdeversion.h>
#if ! ( KDE_IS_VERSION( 3, 2, 90 ) )
#error KDE 3.2
#endif
],
    need_kde32_compat="no"
,
    need_kde32_compat="yes"
)

AC_COMPILE_IFELSE([
#include <kdeversion.h>
#if ! ( KDE_IS_VERSION( 3, 1, 90 ) )
#error KDE 3.1
#endif
],
    need_kde31_compat="no"
,
    need_kde31_compat="yes"
)

CXXFLAGS="$kdeversion_save_CXXFLAGS"
LIBS="$kdeversion_safe_LIBS"
AC_LANG_RESTORE

if test "$need_kde32_compat" = "yes"; then
    AC_MSG_RESULT([KDE 3.2.x])
fi

if test "$need_kde31_compat" = "yes"; then
    AC_MSG_RESULT([KDE 3.1.x])
fi

AM_CONDITIONAL(need_kde33_compatibility, test "$need_kde33_compat" = "yes")
AM_CONDITIONAL(need_kde32_compatibility, test "$need_kde32_compat" = "yes")
AM_CONDITIONAL(need_kde31_compatibility, test "$need_kde31_compat" = "yes")

